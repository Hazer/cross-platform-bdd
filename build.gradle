plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
}

ext {
    cucumberVersion = "6.5.1"
    appiumVersion = "7.3.0"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "io.cucumber:cucumber-java8:$cucumberVersion"
    testImplementation "io.cucumber:cucumber-junit:$cucumberVersion"
    testImplementation "io.cucumber:cucumber-picocontainer:$cucumberVersion"
    testImplementation "io.appium:java-client:$appiumVersion"
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

["Android", "IOS"].each { platform ->
    task("test$platform", type: JavaExec) {
        main = "io.cucumber.core.cli.Main"
        classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
        args '--plugin', 'pretty', '--glue', 'com.example.ltp.list', 'src/test/resources'
        systemProperty 'platform', platform.toLowerCase()
    }
}